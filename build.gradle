plugins {
    id "base"
}

description = 'cipher helper'

allprojects {
    group = 'com.github.120011676'
    version = '0.0.7'

    repositories {
        mavenCentral()
    }

    tasks.withType(JavaCompile) {
        options.encoding = "UTF-8"
    }
}

subprojects {
    apply plugin: 'java-library'
    apply plugin: 'maven-publish'
    apply plugin: 'signing'

    sourceCompatibility = 1.8

    java {
        withJavadocJar()
        withSourcesJar()
    }

    publishing {
        publications {
            mavenJava(MavenPublication) {
                from components.java

                pom {
                    name = 'vine'
                    description = 'cipher helper'
                    url = 'https://github.com/120011676/cipher-helper'
                    licenses {
                        license {
                            name = 'The MIT License (MIT)'
                            url = 'https://mit-license.org'
                        }
                    }
                    developers {
                        developer {
                            id = '120011676'
                            name = 'Say.li'
                            email = '120011676@qq.com'
                        }
                    }
                    scm {
                        connection = 'git@github.com:120011676/cipher-helper.git'
                        developerConnection = 'git@github.com:120011676/cipher-helper.git'
                        url = 'https://github.com/120011676/cipher-helper'
                    }
                }
            }
        }

        repositories {
            maven {
                url "https://oss.sonatype.org/service/local/staging/deploy/maven2"
                credentials {
                    username = maven_username
                    password = maven_password
                }
            }
        }
    }

    signing {
        sign publishing.publications.mavenJava
    }
}
